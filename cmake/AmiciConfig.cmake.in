@PACKAGE_INIT@

include(CMakeFindDependencyMacro)

# Current SUNDIALSConfig.cmake doesn't use KLU's FindKLU, but hardcodes paths,
# and is not relocatable. This does not work with Python package installation in
# tmpdirs.
list(APPEND CMAKE_PREFIX_PATH
     @CMAKE_SOURCE_DIR@/ThirdParty/SuiteSparse/install/)

set(AMICI_BLAS_USE_SCIPY_OPENBLAS @AMICI_BLAS_USE_SCIPY_OPENBLAS@)
include("${CMAKE_CURRENT_LIST_DIR}/AmiciFindBLAS.cmake")

if(@OpenMP_CXX_FOUND@)
  # For unclear reasons, OpenMP is not always found correctly, in particular
  #  on macOS under conda during model compilation.
  #  In that case, we fall back to the values found by the main project.
  #  This is not portable, but we aren't distributing binaries anyway.

  # Try to find OpenMP again, but don't fail if not found
  find_package(OpenMP COMPONENTS CXX)

  if (NOT OpenMP_CXX_FOUND)
    # Fall back to what was found by the main project
    set(OpenMP_CXX_FLAGS @OpenMP_CXX_FLAGS@)
    set(OpenMP_CXX_LIB_NAMES @OpenMP_CXX_LIB_NAMES@)
    set(OpenMP_CXX_INCLUDE_DIRS @OpenMP_CXX_INCLUDE_DIRS@)
    set(OpenMP_CXX_LIBRARIES @OpenMP_CXX_LIBRARIES@)
    set(OpenMP_CXX_FOUND TRUE)
    add_library(OpenMP::OpenMP_CXX INTERFACE IMPORTED)
    set_target_properties(OpenMP::OpenMP_CXX PROPERTIES
      INTERFACE_COMPILE_OPTIONS "${OpenMP_CXX_FLAGS}"
      INTERFACE_LINK_LIBRARIES "${OpenMP_CXX_LIBRARIES}"
      INTERFACE_INCLUDE_DIRECTORIES "${OpenMP_CXX_INCLUDE_DIRS}"
    )
  endif()
endif()

find_dependency(SUNDIALS REQUIRED PATHS
             "@CMAKE_SOURCE_DIR@/ThirdParty/sundials/build/@CMAKE_INSTALL_LIBDIR@/cmake/sundials/")

if(@Boost_CHRONO_FOUND@)
  find_dependency(Boost COMPONENTS chrono REQUIRED)
endif()

if(@HDF5_FOUND@)
  find_dependency(
    HDF5
    COMPONENTS C HL CXX
    REQUIRED)
endif()

include("${CMAKE_CURRENT_LIST_DIR}/AmiciTargets.cmake")

check_required_components(Amici)
